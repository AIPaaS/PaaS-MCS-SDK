dependencies{
	// https://mvnrepository.com/artifact/redis.clients/jedis
	compile group: 'redis.clients', name: 'jedis', version: '3.0.1'
	
	compile 'org.apache.commons:commons-pool2:2.6.2'
	compile 'org.apache.logging.log4j:log4j-api:2.11.2'
	compile 'com.ai:ipaas-uac:1.0'
	compile 'io.codis.jodis:jodis:0.5.1'
	// https://mvnrepository.com/artifact/io.lettuce/lettuce-core
	compile group: 'io.lettuce', name: 'lettuce-core', version: '5.1.7.RELEASE'
}

publishing {
	publications {
		publishing.publications.create("${moduleName}", MavenPublication) {
			groupId group
			artifactId moduleName
			version moduleVersion
			artifact  jar

			artifact (sourcesJar) { classifier "sources" }

			pom.withXml {
				//Creating additional node for dependencies
				def dependenciesNode = asNode().appendNode('dependencies')

				//Defining configuration names from which dependencies will be taken (debugCompile or releaseCompile and compile)
				def configurationNames = ['compile']

				configurationNames.each { configurationName ->
					configurations[configurationName].allDependencies.each {
						if (it.group != null && it.name != null) {
							def dependencyNode = dependenciesNode.appendNode('dependency')
							dependencyNode.appendNode('groupId', it.group)
							dependencyNode.appendNode('artifactId', it.name)
							dependencyNode.appendNode('version', it.version)

							//If there are any exclusions in dependency
							if (it.excludeRules.size() > 0) {
								def exclusionsNode = dependencyNode.appendNode('exclusions')
								it.excludeRules.each { rule ->
									def exclusionNode = exclusionsNode.appendNode('exclusion')
									exclusionNode.appendNode('groupId', rule.group)
									exclusionNode.appendNode('artifactId', rule.module)
								}
							}
						}
					}
				}
			}
		}
	}
	repositories {
		maven {
			credentials {
				username = repoUser
				password = repoPassword
			}
			if(moduleVersion.endsWith('-SNAPSHOT')) {
				url publishSnapShotsURL
			} else {
				url publishReleaseURL
			}
		}
	}
}

